
#Область ПрограммныйИнтерфейс

// Получает действующий тариф на Хаб на заданную дату (месяц)
//
// Параметры:
//  СреднедневнойОборот  - Число - Среднедневное количество единиц, проходящий через хаб 
//  						(КоличетсвоНаХабеЗаМесяц/КоличествоКалендарныхДней)
//  МесяцСреза  - Дата - Любая дата месяца из корого нужно получить тариф на Хаб
//
// Возвращаемое значение:
//   Число   - значение действующего тарифа
//
Функция ПолучитьТарифПоСреднедневномуОбороту(СреднедневнойОборот, МесяцСреза = Неопределено)Экспорт
	
	Если МесяцСреза = Неопределено Тогда 
		
		МесяцСреза = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;                                       
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорогиУстановкиТарифовНаХабыПороги.НижняяГраница КАК НижняяГраница,
	|	ПорогиУстановкиТарифовНаХабыПороги.Тариф КАК Тариф
	|ПОМЕСТИТЬ втПороги
	|ИЗ
	|	Справочник.ВариантыПороговУстановкиТарифовНаХабы.Пороги КАК ПорогиУстановкиТарифовНаХабыПороги
	|ГДЕ
	|	ПорогиУстановкиТарифовНаХабыПороги.Ссылка В
	|			(ВЫБРАТЬ
	|				ТарифыНаХабыСрезПоследних.Порог КАК Порог
	|			ИЗ
	|				РегистрСведений.ТарифыНаХабы.СрезПоследних(&МесяцСреза, ) КАК ТарифыНаХабыСрезПоследних)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПороги.НижняяГраница КАК НижняяГраница,
	|	втПороги.Тариф КАК Тариф
	|ИЗ
	|	втПороги КАК втПороги
	|ГДЕ
	|	втПороги.НижняяГраница <= &СреднедневнойОборот
	|
	|УПОРЯДОЧИТЬ ПО
	|	НижняяГраница УБЫВ
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("СреднедневнойОборот", СреднедневнойОборот);
	Запрос.УстановитьПараметр("МесяцСреза", МесяцСреза); 
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Тариф;
	КонецЕсли;
	
	Возврат 0;
КонецФункции // ПолучитьТарифПоСреднедневномуОбороту()


// Получает действующий вариант порогов для Хаб на заданную дату (месяц)
//
// Параметры:
//  МесяцСреза  - Дата - Любая дата месяца из корого нужно получить тариф на Хаб
//
// Возвращаемое значение:
//   СправочникСсылка.ВариантыПороговУстановкиТарифовНаХабы, неопределено   - значение действующего порога
//
Функция ПолучитьДействующийВариантПорогов(МесяцСреза = Неопределено)Экспорт
	
	Если МесяцСреза = Неопределено Тогда 
		
		МесяцСреза = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;                                       
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТарифыНаХабыСрезПоследних.Порог КАК Порог
	|ИЗ
	|	РегистрСведений.ТарифыНаХабы.СрезПоследних(&МесяцСреза, ) КАК ТарифыНаХабыСрезПоследних";
		
	Запрос.УстановитьПараметр("МесяцСреза", МесяцСреза); 
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Порог;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции // ПолучитьДействующийВариантПорогов()

#КонецОбласти