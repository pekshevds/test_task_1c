#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает таблицу действующих тариф на заданный месяц
//
// Параметры:
//  КодыТиповОбработки  - Массив из ПеречислениеСсылка.КодыТиповОбработки - Любой код типа обработки, кроме Хаб
//  МесяцСреза  - Дата - Любая дата месяца из корого нужно получить тариф
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица тарифов (КодТипаОбработки (Перечисление.КодыТиповОбработки), 
//   					 Тариф (Число(15, 2)))
//
Функция ПолучитьТаблицуТарифов(КодыТиповОбработки, МесяцСреза = Неопределено)Экспорт
	
	Если МесяцСреза = Неопределено Тогда 
		
		МесяцСреза = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;                                       
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТарифыНаВсеКромеХабовСрезПоследних.КодТипаОбработки КАК КодТипаОбработки,
	|	ТарифыНаВсеКромеХабовСрезПоследних.Тариф КАК Тариф
	|ИЗ
	|	РегистрСведений.ТарифыНаВсеКромеХабов.СрезПоследних(&МесяцСреза, КодТипаОбработки В (&КодыТиповОбработки)) КАК ТарифыНаВсеКромеХабовСрезПоследних";
	
	Запрос.УстановитьПараметр("КодыТиповОбработки", КодыТиповОбработки);
	Запрос.УстановитьПараметр("МесяцСреза", МесяцСреза);
	
	Результат = Запрос.Выполнить();
	ТаблицаТарифов = Результат.Выгрузить();	
	ТаблицаТарифов.Индексы.Добавить("КодТипаОбработки");
	
	Возврат ТаблицаТарифов;
КонецФункции // ПолучитьТаблицуТарифов()


// Получает действущий на указанный месяц тариф по коду типа обработки
//
// Параметры:
//  КодТипаОбработки  - ПеречислениеСсылка.КодыТиповОбработки - Любой код типа обработки, кроме Хаб
//  МесяцСреза  - Дата - Любая дата месяца из корого нужно получить тариф
//
// Возвращаемое значение:
//   Число   - значение действующего тарифа
//
Функция ПолучитьТариф(КодТипаОбработки, МесяцСреза = Неопределено)Экспорт
	
	КодыТиповОбработки = Новый Массив;
	КодыТиповОбработки.Добавить(КодТипаОбработки);
	
	ТаблицаТарифов = ПолучитьТаблицуТарифов(КодыТиповОбработки, МесяцСреза);
	Если ТаблицаТарифов.Количество() > 0 Тогда 
		
		Возврат ТаблицаТарифов[0].Тариф;
	КонецЕсли;
	
	Возврат 0;
КонецФункции // ПолучитьТариф()

// Получает действущий на указанный месяц тариф за хранение
//
// Параметры:
//  МесяцСреза  - Дата - Любая дата месяца из корого нужно получить тариф
//
// Возвращаемое значение:
//   Число   - значение действующего тарифа
//
Функция ПолучитьТарифХранения(МесяцСреза = Неопределено)Экспорт
	
	Возврат ПолучитьТариф(Перечисления.КодыТиповОбработки.Хранение, МесяцСреза);	
КонецФункции // ПолучитьТарифХранения()

#КонецОбласти

#КонецЕсли
  