
#Область ПрограммныйИнтерфейс

// Запускает механизм расчета документов за месяц
//
// Параметры:  
//  ДатаМесяца  - Дата - дата месяца для расчета тарифов
//
Процедура ВыполнитьРасчетЗаМесяц(ДатаМесяца)Экспорт 
	
	ВыборкаДокументовЗаМесяц = Документы.УслугаСортировки.ПолучитьВыборкуДокументовЗаМесяц(ДатаМесяца);
	Корзины = МетодыРаботыСДокументамиУслугаСортировки.РазбитьВыборкуДокументовНаЧасти(ВыборкаДокументовЗаМесяц);
	Для Каждого Корзина Из Корзины Цикл 
		
		Ключ = Новый УникальныйИдентификатор;
		
		ПараметрыВыполнения = Новый Массив;
		ПараметрыВыполнения.Добавить(Корзина);
		ПараметрыВыполнения.Добавить(ДатаМесяца);
		ФоновыеЗадания.Выполнить(
			"РегламентныеИФоновыеЗадания.ВыполнитьРасчетЗаМесяцВФоновомЗадании", 
			ПараметрыВыполнения, Ключ);
	КонецЦикла;
КонецПроцедуры // ВыполнитьРасчетЗаМесяц()


// Запускает фоновые задания для загрузки услуг сортировки
//
// Параметры:
//  АдресФайлаВоВременномХранилище  - Строка - адрес нахождения файла
//                 во временном хранилище
//
Процедура ВыполнитьЗагрузкуУслугСортировки(АдресФайлаВоВременномХранилище)Экспорт 

	ДанныеИзФайла = МетодыРаботыСЗагружаемымФайломИЕгоСодержимымВызовСервера.ПрочитатьДанныеИзФайлаВоВоременномХранилище(
		АдресФайлаВоВременномХранилище);	
   	ЗапуститьЗагрузкуДанных(ДанныеИзФайла);
КонецПроцедуры // ВыполнитьЗагрузкуУслугСортировки()


// Запускает фоновое задание удаления документов УслугаСортировки
//
Процедура ВыполнитьУдалениеУслугСортировкиВФоновомЗадание()Экспорт 
	
	ПараметрыВыполнения = Новый Массив;
		ФоновыеЗадания.Выполнить(
			"РегламентныеИФоновыеЗадания.УдалитьВсеДокументыУслугаСортировки", 
			ПараметрыВыполнения);	
КонецПроцедуры // ВыполнитьУдалениеУслугСортировкиВФоновомЗадание()

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Запускает фоновые задания для загрузки услуг сортировки
//
// Параметры:
//  ДанныеИзФайла  - Массив из Строка - содержимое выгружаемого файла в виде массива строк
//
Процедура ЗапуститьЗагрузкуДанных(ДанныеИзФайла)Экспорт
	ЧастиДанныхФайла = МетодыРаботыСЗагружаемымФайломИЕгоСодержимымВызовСервера.РазбитьДанныеФайлаНаЧасти(ДанныеИзФайла);
	Для Каждого ЧастьДанныхФайла Из ЧастиДанныхФайла Цикл 
		
		
		Ключ = Новый УникальныйИдентификатор;		
		ПараметрыВыполнения = Новый Массив;
		ПараметрыВыполнения.Добавить(ЧастьДанныхФайла);		
		
		ФоновыеЗадания.Выполнить("МетодыРаботыСДокументамиУслугаСортировки.СоздатьДокументыУслугаСортировки", 
				ПараметрыВыполнения, Ключ);
	КонецЦикла;
КонецПроцедуры // ЗапуститьЗагрузкуДанных()

// Запускает фоновое задание для загрузки услуг сортировки
//
// Параметры:
//  АдресФайлаВоВременномХранилище  - Строка - адрес нахождения файла
//                 								во временном хранилище
//
Процедура ВыполнитьЗагрузкуУслугСортировкиВФоновомЗадании(АдресФайлаВоВременномХранилище)Экспорт 

	ПараметрыВыполнения = Новый Массив;	
	ПараметрыВыполнения.Добавить(АдресФайлаВоВременномХранилище);			
	ФоновыеЗадания.Выполнить(
		"РегламентныеИФоновыеЗадания.ВыполнитьЗагрузкуУслугСортировки", 
		ПараметрыВыполнения, Новый УникальныйИдентификатор);
КонецПроцедуры // ВыполнитьЗагрузкуУслугСортировкиВФоновомЗадании()

//
// Удаляет все документы УслугаСортировки
//
Процедура УдалитьВсеДокументыУслугаСортировки()Экспорт 	
	
	ДокументыДляУдаления = МетодыРаботыСДокументамиУслугаСортировки.ПолучитьNДокументов();
	Пока ДокументыДляУдаления.Количество() > 0 Цикл 
		
		УдалитьОбъекты(ДокументыДляУдаления);
		ДокументыДляУдаления = МетодыРаботыСДокументамиУслугаСортировки.ПолучитьNДокументов();
	КонецЦикла;
КонецПроцедуры // УдалитьВсеДокументыУслугаСортировки()


// Рассчитывает часть документов (заполняет СтоимостьХранения и СтоимостьСортировки)
//
// Параметры:
//  Корзина  - Массив из ДокументСсылка.УслугаСортировки - Список документов для рассчета
//  ДатаМесяца  - Дата - дата месяца для расчета тарифов
//
Процедура ВыполнитьРасчетЗаМесяцВФоновомЗадании(Корзина, ДатаМесяца)Экспорт 
	КоличествоПопыток = 5;	
	
	Тарифы = ПолучитьТарифы(ДатаМесяца);
	Для Каждого УслугаСортировки Из Корзина Цикл 
		
			
		Счетчик = 1;
		Пока Не МетодыРаботыСДокументамиУслугаСортировки.РассчитатьДокументУслугаСортировки(
			УслугаСортировки, Тарифы) Цикл 
			
			Если Счетчик = КоличествоПопыток Тогда 
				
				Прервать;
			КонецЕсли;
			Счетчик = Счетчик + 1;			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // ВыполнитьРасчетЗаМесяцВФоновомЗадании()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет тарифы на все коды обработки
//
// Параметры:
//  ДатаМесяца  - Дата - День месяца, на котрый нужно сформировать тарифы  
//
// Возвращаемое значение:
//   Соответствие из ПеречислениеСсылка.КодыТиповОбработки - Тарифы
//
Функция ПолучитьТарифы(ДатаМесяца)
	
	Соответствие = Новый Соответствие;
	
	ТаблицаСреднихКоличествЕдиницПроходящихЧерезХабЗаМесяц = 
		Документы.УслугаСортировки.ПолучитьТаблицуСреднихКоличествЕдиницПроходящихЧерезХабЗаМесяц(ДатаМесяца);
		
	ТарифыНаХабы = РегистрыСведений.ТарифыНаХабы.ПолучитьТарифыПоСреднедневномуОбороту(
		ТаблицаСреднихКоличествЕдиницПроходящихЧерезХабЗаМесяц, ДатаМесяца);
	Соответствие.Вставить(Перечисления.КодыТиповОбработки.Хаб, ТарифыНаХабы);
	
	ТарифХранения = РегистрыСведений.ТарифыНаВсеКромеХабов.ПолучитьТарифХранения(ДатаМесяца);
	Соответствие.Вставить(Перечисления.КодыТиповОбработки.Хранение, ТарифХранения);
	
	ТарифНаШины = РегистрыСведений.ТарифыНаВсеКромеХабов.ПолучитьТариф(
			Перечисления.КодыТиповОбработки.Шины, ДатаМесяца);
	Соответствие.Вставить(Перечисления.КодыТиповОбработки.Шины, ТарифНаШины);
	
	ТарифНаКГТ = РегистрыСведений.ТарифыНаВсеКромеХабов.ПолучитьТариф(
			Перечисления.КодыТиповОбработки.КГТ, ДатаМесяца);
	Соответствие.Вставить(Перечисления.КодыТиповОбработки.КГТ, ТарифНаКГТ);
	
	ТарифНаМГТ = РегистрыСведений.ТарифыНаВсеКромеХабов.ПолучитьТариф(
			Перечисления.КодыТиповОбработки.МГТ, ДатаМесяца);
	Соответствие.Вставить(Перечисления.КодыТиповОбработки.МГТ, ТарифНаМГТ);
	
	Возврат Соответствие;
КонецФункции // ПолучитьТарифы()

#КонецОбласти