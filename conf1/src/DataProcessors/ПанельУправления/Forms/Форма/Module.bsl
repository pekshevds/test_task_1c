#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РасширениеРаботыСФайламиПодключено = Истина;
	
	
	#Если ВебКлиент Тогда
	ПодключитьРасширенияРаботыСФайлами(Истина);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) 
		
	СтандартнаяОбработка = Ложь;
	Если Не РасширениеРаботыСФайламиПодключено Тогда 
		
		ПоказатьПредупреждение(, "Не подключено расширение работы с файлами", 10, "Критическая ошибка");
		Возврат;
	КонецЕсли;
	
	
	РежимРаботы = РежимДиалогаВыбораФайла.Открытие; 	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимРаботы);
	
	ДиалогВыбораФайла.Фильтр                      = НСтр("ru = 'Dataset'") + " (*.csv)|*.csv";
	ДиалогВыбораФайла.Заголовок                   = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
	ДиалогВыбораФайла.Расширение                  = "csv";
	ДиалогВыбораФайла.ИндексФильтра               = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла              = "dataset.csv";
	ДиалогВыбораФайла.МножественныйВыбор		  = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаДляЗагрузкиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайла(Команда)
	
	Если Не РасширениеРаботыСФайламиПодключено Тогда 
		
		ПоказатьПредупреждение(, "Не подключено расширение работы с файлами", 10, "Критическая ошибка");
		Возврат;
	КонецЕсли;    
	
	Если ПутьКФайлу = "" Тогда 
		
		ПоказатьПредупреждение(, "Не указан путь к файлу", 10, "Критическая ошибка");
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПомещениеФайлаНаСерверЗавершение", 
		ЭтотОбъект);
	ОписаниеОповещенияОХодеВыполнения = Новый ОписаниеОповещения("ПомещениеФайлаНаСерверХодВыполнения", 
		ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ОписаниеОповещенияОЗавершении, ОписаниеОповещенияОХодеВыполнения, , , 
		ПутьКФайлу, УникальныйИдентификатор);	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВсеДокументы(Команда)
	
	УдалитьВсеДокументыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОбработкуДокументов(Команда)
	
	ЗапуститьОбработкуДокументовНаСервере();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура УстановитьДоступностьКнопкиЗагрузитьДанныеИзФайла(Доступность = Истина)
	
	Элементы.ЗагрузитьДанныеИзФайла.Доступность = Доступность;
КонецПроцедуры

&НаКлиенте 
Процедура ПодключитьРасширенияРаботыСФайлами(УстановитьЕслиНеПодключено) Экспорт
    НачатьПодключениеРасширенияРаботыСФайлами(
        Новый ОписаниеОповещения(
            "ПослеПодключенияРасширенияРаботыСФайлами",
            ЭтотОбъект,
            УстановитьЕслиНеПодключено));
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияРасширенияРаботыСФайлами(Подключено, УстановитьЕслиНеПодключено) Экспорт
    Если Подключено Тогда
        РасширениеРаботыСФайламиПодключено = Истина;

    ИначеЕсли УстановитьЕслиНеПодключено Тогда
        НачатьУстановкуРасширенияРаботыСФайлами(
            Новый ОписаниеОповещения(
                "ПодключитьРасширенияРаботыСФайлами",
                ЭтотОбъект,
                Ложь));
    Иначе
        РасширениеРаботыСФайламиПодключено = Ложь;		
    КонецЕсли;    
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаДляЗагрузкиЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено
		И ВыбранныеФайлы.Количество() > 0 Тогда
		
		ПутьКФайлу = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеФайлаНаСерверЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры)Экспорт 
	
	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьДоступностьКнопкиЗагрузитьДанныеИзФайла();
	
	АдресФайла = ОписаниеПомещенногоФайла.Адрес;
	ЗагрузитьДанныеИзФайлаНаСервере(АдресФайла);
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеФайлаНаСерверХодВыполнения(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ДополнительныеПараметры)Экспорт 
	
	УстановитьДоступностьКнопкиЗагрузитьДанныеИзФайла(Ложь);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеИзФайлаНаСервере(АдресФайлаВоВременномХранилище)
	
	РегламентныеИФоновыеЗадания.ВыполнитьЗагрузкуУслугСортировки(АдресФайлаВоВременномХранилище);	
КонецПроцедуры

&НаСервере
Процедура УдалитьВсеДокументыНаСервере()
	
	РегламентныеИФоновыеЗадания.ВыполнитьУдалениеУслугСортировкиВФоновомЗадание();
КонецПроцедуры

&НаСервере
Процедура ЗапуститьОбработкуДокументовНаСервере()
	
	РегламентныеИФоновыеЗадания.ВыполнитьРасчетЗаМесяц(КонецМесяца(ДатаМесяц));
КонецПроцедуры

#КонецОбласти